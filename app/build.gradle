apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'
  defaultConfig {
    applicationId "io.weicools.purereader"
    minSdkVersion 22
    targetSdkVersion 27
    versionCode 101
    versionName "1.0.1"

    manifestPlaceholders = [UMENG_CHANNEL_VALUE: "GooglePlay"]
    addManifestPlaceholders([APP_NAME: "@string/app_name"])

    ndk {
      abiFilters 'armeabi', 'arm64-v8a'
    }

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
  }

  signingConfigs {
    release {
      storeFile file('../Weicools.jks')
      storePassword '12138@233'
      keyPassword '12138@233'
      keyAlias 'Weicools'
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    release {
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  applicationVariants.all { variant ->
    variant.outputs.all {
      def apkName = 'PureReader' + '-' + variant.versionName
      if (!variant.flavorName.isEmpty()) {
        apkName += ('-' + variant.flavorName)
      }
      outputFileName = apkName + '.apk'
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  // kotlin plugin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  // Support Library
  implementation "com.android.support:design:$support_version"
  implementation "com.android.support:support-v4:$support_version"
  implementation "com.android.support:customtabs:$support_version"
  implementation "com.android.support:cardview-v7:$support_version"
  implementation "com.android.support:appcompat-v7:$support_version"
  implementation "com.android.support:preference-v14:$support_version"
  implementation "com.android.support.constraint:constraint-layout:$constrain_version"

  // Room
  implementation "android.arch.persistence.room:runtime:$room_version"
  implementation "android.arch.persistence.room:rxjava2:$room_version"
  annotationProcessor "android.arch.persistence.room:compiler:$room_version"

  // Rx series
  implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
  implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

  // Retrofit series
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

  // json
  implementation "com.google.code.gson:gson:$gson_version"

  // Image load
  implementation "com.facebook.fresco:fresco:$fresco_version"
  implementation "com.github.bumptech.glide:glide:$glide_version"
  annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

  // ButterKnife
  implementation "com.jakewharton:butterknife:$butterknife_version"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

  // ui library
  implementation "com.aurelhubert:ahbottomnavigation:$ahbottom_version"
  implementation "com.github.daniel-stoneuk:material-about-library:$about_version"

  // analyze
  implementation "com.umeng.analytics:analytics:$umeng_version"

  // leakCanary
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_version"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_version"
}
